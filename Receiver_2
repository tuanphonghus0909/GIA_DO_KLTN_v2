#include <Arduino.h>
#include <esp_now.h>
#include <WiFi.h>
#include <esp_wifi.h>
#include <WiFiClientSecure.h>
#include <PubSubClient.h>
#include <Adafruit_GFX.h>
#include <Adafruit_ST7735.h>
#include <SPI.h>
#include "secrets/wifi.h"
#include "secrets/mqtt.h"
#include "ca_cert.h"
#include "MQTT.h"
#include "wifi_connect.h"

// ====== Forward Declarations ======
void sendResetToAll();
void updateTFT(int nodeIdx);
void OnDataRecv(const uint8_t *mac, const uint8_t *data, int len);
void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status);
uint8_t countActiveNodes();

// ====== TFT PINS ======
#define TFT_CS     2
#define TFT_RST    5
#define TFT_DC     4
#define TFT_BL     22
Adafruit_ST7735 tft = Adafruit_ST7735(TFT_CS, TFT_DC, TFT_RST);
#define SCREEN_W   160
#define SCREEN_H   128

// ====== RGB LED PINS ======
#define LED_RED    27
#define LED_GREEN  26
#define LED_BLUE   25
#define PWM_FREQ   5000
#define PWM_RES    8
#define RED_CH     0
#define GREEN_CH   1
#define BLUE_CH    2

#define TOUCH_PIN 32
#define TOUCH_THRESHOLD 40

uint8_t senderAddresses[][6] = {
  {0xDC, 0x06, 0x75, 0x67, 0x77, 0xFC}, // Node 2
  {0xDC, 0x06, 0x75, 0x67, 0x67, 0xE0}, // Node 1
  {0x50, 0x78, 0x7D, 0x42, 0xE2, 0x48}, // Node 3
  {0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF}, // Node 4 
  {0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF}, // Node 5 
  {0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF}  // Node 6 
};
const int NUM_SENDERS = 6;

uint8_t broadcastAddress[] = {0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF};

const uint8_t PMK_KEY[16] = {'p', 'm', 'k', '1', '2', '3', '4', '5', '6', '7', '8', '9', '0', '1', '2', '3'};
const uint8_t LMK_KEYS[][16] = {
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '2', '_', '1', '2', '3', '4', '5', '6'}, // LMK for Node 2
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '1', '_', '1', '2', '3', '4', '5', '6'}, // LMK for Node 1
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '3', '_', '1', '2', '3', '4', '5', '6'}, // LMK for Node 3
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '4', '_', '1', '2', '3', '4', '5', '6'}, // LMK for Node 4
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '5', '_', '1', '2', '3', '4', '5', '6'}, // LMK for Node 5
  {'l', 'm', 'k', '_', 'n', 'o', 'd', 'e', '6', '_', '1', '2', '3', '4', '5', '6'}  // LMK for Node 6
};

#define TDMA_THRESHOLD 6
#define SLOT_DURATION 2000  // 2s per node
#define SYNC_INTERVAL 300000
#define NUM_SAMPLES 6

#define ROW_HEIGHT 32
#define COL_NODE   40
#define COL_ROLL   38
#define COL_PITCH  38
#define COL_BATT   44

typedef struct struct_message {
  char a[32];
  float roll[NUM_SAMPLES];
  float pitch[NUM_SAMPLES];
  bool reset;
  float battery_percentage;
} struct_message;

typedef struct sync_message {
  uint32_t timestamp;
  uint32_t cycle_start;
  uint8_t num_nodes;
} sync_message;

struct_message incomingData;
struct_message resetMessage;
sync_message syncMsg;
struct_message nodeData[NUM_SENDERS];

// ====== NODE TRACKING ======
bool nodeActive[NUM_SENDERS] = {false};
unsigned long lastNodeTime[NUM_SENDERS] = {0};
const unsigned long NODE_TIMEOUT = 60000;

// ====== TFT COLORS ======
#define BLACK      0x0000
#define WHITE      0xFFFF
#define RED        0xF800
#define GREEN      0x07E0
#define YELLOW     0xFFE0
#define CYAN       0x07FF

// ====== MQTT CONFIG ======
WiFiClientSecure tlsClient;
PubSubClient mqttClient(tlsClient);
const char *resetTopic = "esp32/reset";
const char *subscribeTopics[] = {resetTopic};
char mqttBuf[10];
float prevRoll[NUM_SENDERS] = {0};
float prevPitch[NUM_SENDERS] = {0};
float prevBattery[NUM_SENDERS] = {0};
const float mqttRollPitchThreshold = 0.1;
const float mqttBatteryThreshold = 0.5;

void mqttCallback(char *topic, uint8_t *payload, unsigned int len) {
  char msg[len + 1];
  memcpy(msg, payload, len);
  msg[len] = '\0';
  Serial.printf("MQTT received on %s: %s\n", topic, msg);
  if (strcmp(topic, resetTopic) == 0 && strcmp(msg, "RESET") == 0) {
    Serial.println("Received RESET command via MQTT");
    sendResetToAll();
  }
}

void initTFT() {
  pinMode(TFT_BL, OUTPUT);
  digitalWrite(TFT_BL, HIGH);
  tft.initR(INITR_BLACKTAB);
  SPI.setFrequency(12000000);
  tft.setRotation(1);
  tft.fillScreen(BLACK);

  // Draw table grid for header + 3 nodes
  for (int i = 0; i <= 3; i++) { // y=0, 32, 64, 96
    tft.drawFastHLine(0, i * ROW_HEIGHT, SCREEN_W, WHITE);
  }
  int xPos[] = {0, COL_NODE, COL_NODE + COL_ROLL, COL_NODE + COL_ROLL + COL_PITCH, SCREEN_W};
  for (int x : xPos) {
    tft.drawFastVLine(x, 0, SCREEN_H, WHITE);
  }

  // Draw header
  tft.setTextSize(1);
  tft.setCursor(5, 12);
  tft.setTextColor(WHITE);
  tft.println("Node");
  tft.setCursor(COL_NODE + 5, 12);
  tft.println("Roll");
  tft.setCursor(COL_NODE + COL_ROLL + 5, 12);
  tft.println("Pitch");
  tft.setCursor(COL_NODE + COL_ROLL + COL_PITCH + 5, 12);
  tft.println("Batt");

  // Initialize Nodes 1–3
  for (int i = 0; i < 3; i++) {
    updateTFT(i);
  }
}

void drawBatteryIcon(int x, int y, float percentage) {
  tft.fillRect(x, y, 16, 6, BLACK);
  uint16_t color = percentage > 70 ? GREEN : (percentage >= 30 ? YELLOW : RED);
  tft.drawRect(x, y, 14, 6, WHITE);
  tft.fillRect(x + 14, y + 2, 2, 2, WHITE);
  int fillWidth = (int)(12 * (percentage / 100.0));
  if (fillWidth > 0) {
    tft.fillRect(x + 1, y + 1, fillWidth, 4, color);
  }
}

void updateTFT(int nodeIdx) {
  Serial.printf("updateTFT called for Node%d\n", nodeIdx + 1);
  int yBase = (nodeIdx + 1) * ROW_HEIGHT;
  // Clear row (except grid lines)
  tft.fillRect(1, yBase + 1, COL_NODE - 1, ROW_HEIGHT - 1, BLACK);
  tft.fillRect(COL_NODE + 1, yBase + 1, COL_ROLL - 1, ROW_HEIGHT - 1, BLACK);
  tft.fillRect(COL_NODE + COL_ROLL + 1, yBase + 1, COL_PITCH - 1, ROW_HEIGHT - 1, BLACK);
  tft.fillRect(COL_NODE + COL_ROLL + COL_PITCH + 1, yBase + 1, COL_BATT - 1, ROW_HEIGHT - 1, BLACK);

  // Node label
  tft.setTextColor(CYAN);
  tft.setTextSize(1);
  tft.setCursor(5, yBase + 12);
  tft.printf("Node%d", nodeIdx + 1);

  if (!nodeActive[nodeIdx]) {
    tft.setTextColor(WHITE);
    tft.setCursor(COL_NODE + 5, yBase + 12);
    tft.println("---");
    tft.setCursor(COL_NODE + COL_ROLL + 5, yBase + 12);
    tft.println("---");
    tft.setCursor(COL_NODE + COL_ROLL + COL_PITCH + 10, yBase + 16);
    tft.println("0.0%");
    drawBatteryIcon(COL_NODE + COL_ROLL + COL_PITCH + 10, yBase + 8, 0);
    Serial.printf("Node%d inactive, displayed: ---, ---, 0.0%%\n", nodeIdx + 1);
    return;
  }

  // Use 6th sample (index 5)
  float lastRoll = nodeData[nodeIdx].roll[5];
  float lastPitch = nodeData[nodeIdx].pitch[5];
  float battery = nodeData[nodeIdx].battery_percentage;

  // Display roll and pitch
  tft.setTextColor(WHITE);
  tft.setTextSize(1);
  tft.setCursor(COL_NODE + 2, yBase + 12);
  tft.printf("%+.2f\xF8", lastRoll);
  tft.setCursor(COL_NODE + COL_ROLL + 2, yBase + 12);
  tft.printf("%+.2f\xF8", lastPitch);

  // Display battery
  drawBatteryIcon(COL_NODE + COL_ROLL + COL_PITCH + 10, yBase + 8, battery);
  tft.setCursor(COL_NODE + COL_ROLL + COL_PITCH + 10, yBase + 16);
  tft.printf("%.1f%%", battery);

  Serial.printf("Node%d displayed: Roll=%.2f°, Pitch=%.2f°, Battery=%.1f%%\n", 
                nodeIdx + 1, lastRoll, lastPitch, battery);

  // MQTT publish
  if (mqttClient.connected()) {
    char topic[32];
    snprintf(topic, sizeof(topic), "esp32/node%d/roll", nodeIdx + 1);
    if (fabs(lastRoll - prevRoll[nodeIdx]) > mqttRollPitchThreshold) {
      snprintf(mqttBuf, sizeof(mqttBuf), "%.2f", lastRoll);
      mqttClient.publish(topic, mqttBuf, false);
      prevRoll[nodeIdx] = lastRoll;
      Serial.printf("Published to %s: %.2f\n", topic, lastRoll);
    }

    snprintf(topic, sizeof(topic), "esp32/node%d/pitch", nodeIdx + 1);
    if (fabs(lastPitch - prevPitch[nodeIdx]) > mqttRollPitchThreshold) {
      snprintf(mqttBuf, sizeof(mqttBuf), "%.2f", lastPitch);
      mqttClient.publish(topic, mqttBuf, false);
      prevPitch[nodeIdx] = lastPitch;
      Serial.printf("Published to %s: %.2f\n", topic, lastPitch);
    }

    snprintf(topic, sizeof(topic), "esp32/node%d/battery", nodeIdx + 1);
    if (fabs(battery - prevBattery[nodeIdx]) > mqttBatteryThreshold) {
      snprintf(mqttBuf, sizeof(mqttBuf), "%.1f", battery);
      mqttClient.publish(topic, mqttBuf, false);
      prevBattery[nodeIdx] = battery;
      Serial.printf("Published to %s: %.1f\n", topic, battery);
    }
  } else {
    Serial.println("MQTT not connected, skipping publish");
  }
}

// ====== RGB LED FUNCTIONS ======
void initRGB() {
  ledcSetup(RED_CH, PWM_FREQ, PWM_RES);
  ledcSetup(GREEN_CH, PWM_FREQ, PWM_RES);
  ledcSetup(BLUE_CH, PWM_FREQ, PWM_RES);
  ledcAttachPin(LED_RED, RED_CH);
  ledcAttachPin(LED_GREEN, GREEN_CH);
  ledcAttachPin(LED_BLUE, BLUE_CH);
  ledcWrite(RED_CH, 255);
  ledcWrite(GREEN_CH, 255);
  ledcWrite(BLUE_CH, 255);
}

void setRGBColor(uint8_t r, uint8_t g, uint8_t b) {
  ledcWrite(RED_CH, 255 - r);
  ledcWrite(GREEN_CH, 255 - g);
  ledcWrite(BLUE_CH, 255 - b);
}

void updateRGB() {
  bool redCondition = false, yellowCondition = false;
  for (int i = 0; i < NUM_SENDERS; i++) {
    if (!nodeActive[i]) continue;
    float maxVal = 0;
    for (int j = 0; j < NUM_SAMPLES; j++) {
      maxVal = max(maxVal, max(fabs(nodeData[i].roll[j]), fabs(nodeData[i].pitch[j])));
    }
    if (i == 2) { // Node 3
      if (maxVal >= 20) redCondition = true;
      else if (maxVal >= 10) yellowCondition = true;
    } else { // Node 1, Node 2
      if (maxVal >= 10) redCondition = true;
      else if (maxVal >= 5) yellowCondition = true;
    }
  }
  if (redCondition) {
    setRGBColor(255, 0, 0);
  } else if (yellowCondition) {
    setRGBColor(255, 255, 0);
  } else {
    setRGBColor(0, 255, 0);
  }
}

// ====== ESP-NOW FUNCTIONS ======
void initESPNow() {
  WiFi.mode(WIFI_STA);
  esp_wifi_start();
  WiFi.disconnect();
  int channel = 1;
  esp_wifi_set_channel(channel, WIFI_SECOND_CHAN_NONE);
  Serial.print("Wi-Fi Channel: ");
  Serial.println(channel);

  if (esp_now_init() != ESP_OK) {
    Serial.println("ESP-NOW init failed");
    while (1);
  }
  Serial.println("ESP-NOW initialized successfully");

  if (esp_now_set_pmk(PMK_KEY) != ESP_OK) {
    Serial.println("Failed to set PMK");
    while (1);
  }

  esp_now_register_recv_cb(OnDataRecv);
  esp_now_register_send_cb(OnDataSent);
  Serial.println("Callbacks registered");
}

void addPeers() {
  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, broadcastAddress, 6);
  peerInfo.channel = 1;
  peerInfo.encrypt = false;
  if (esp_now_add_peer(&peerInfo) != ESP_OK) {
    Serial.println("Failed to add broadcast peer");
    while (1);
  }

  for (int i = 0; i < NUM_SENDERS; i++) {
    esp_now_peer_info_t peerInfo = {};
    memcpy(peerInfo.peer_addr, senderAddresses[i], 6);
    peerInfo.channel = 1;
    peerInfo.encrypt = true;
    memcpy(peerInfo.lmk, LMK_KEYS[i], 16);
    if (esp_now_add_peer(&peerInfo) != ESP_OK) {
      Serial.println("Failed to add peer for Node " + String(i + 1));
      while (1);
    }
    Serial.println("Peer added for Node " + String(i + 1) + " with encryption");
  }
}

void OnDataRecv(const uint8_t *mac, const uint8_t *data, int len) {
  Serial.println("Packet received!");
  if (len == sizeof(incomingData)) {
    memcpy(&incomingData, data, sizeof(incomingData));
    int nodeIdx = -1;
    for (int i = 0; i < NUM_SENDERS; i++) {
      if (memcmp(mac, senderAddresses[i], 6) == 0) {
        nodeIdx = i;
        nodeActive[i] = true;
        lastNodeTime[i] = millis();
        memcpy(&nodeData[i], &incomingData, sizeof(incomingData));
        break;
      }
    }
    if (nodeIdx == -1) return;
    if (incomingData.reset) {
      Serial.println(" Received RESET command");
    } else {
      Serial.println(" Received MPU6050 data:");
      Serial.print("  Source: "); Serial.println(incomingData.a);
      for (int i = 0; i < NUM_SAMPLES; i++) {
        Serial.print("  Roll["); Serial.print(i); Serial.print("]: ");
        Serial.print(incomingData.roll[i]); Serial.println(" deg");
        Serial.print("  Pitch["); Serial.print(i); Serial.print("]: ");
        Serial.print(incomingData.pitch[i]); Serial.println(" deg");
      }
      Serial.print("  Battery: "); Serial.print(incomingData.battery_percentage); Serial.println("%");
    }
    Serial.print("  From MAC: ");
    for (int i = 0; i < 6; i++) {
      Serial.printf("%02X", mac[i]);
      if (i < 5) Serial.print(":");
    }
    Serial.println("\n-----------------------------");
    updateTFT(nodeIdx);
    updateRGB();
  } else {
    Serial.println("Invalid packet size!");
  }
}

void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status) {
  Serial.print("Send Status to ");
  for (int i = 0; i < 6; i++) {
    Serial.printf("%02X", mac_addr[i]);
    if (i < 5) Serial.print(":");
  }
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? ": Success" : ": Fail");
}

void sendSyncBeacon() {
  uint8_t activeNodes = countActiveNodes();
  if (activeNodes >= TDMA_THRESHOLD) {
    uint32_t cycleDuration = activeNodes * SLOT_DURATION; // Dynamic cycle: 2s per node
    syncMsg.timestamp = millis();
    syncMsg.cycle_start = syncMsg.timestamp - (syncMsg.timestamp % cycleDuration);
    syncMsg.num_nodes = activeNodes;
    esp_err_t result = esp_now_send(broadcastAddress, (uint8_t *)&syncMsg, sizeof(syncMsg));
    Serial.printf("Sent TDMA sync beacon, num_nodes: %d, cycle_duration: %ldms (Result: %s)\n",
                  syncMsg.num_nodes, cycleDuration, result == ESP_OK ? "OK" : "Error");
  } else {
    Serial.printf("Skipped TDMA sync beacon: %d active nodes < %d threshold\n", activeNodes, TDMA_THRESHOLD);
  }
  updateRGB();
}

void sendResetToAll() {
  for (int i = 0; i < NUM_SENDERS; i++) {
    Serial.println("Sending RESET to Node " + String(i + 1));
    esp_err_t result = esp_now_send(senderAddresses[i], (uint8_t *)&resetMessage, sizeof(resetMessage));
    Serial.print("Sending RESET to ");
    for (int j = 0; j < 6; j++) {
      Serial.printf("%02X", senderAddresses[i][j]);
      if (j < 5) Serial.print(":");
    }
    Serial.print(" (Result: ");
    Serial.print(result == ESP_OK ? "OK" : "Error");
    Serial.println(")");
  }
}

// ====== NODE MANAGEMENT ======
uint8_t countActiveNodes() {
  uint8_t count = 0;
  unsigned long currentTime = millis();
  for (int i = 0; i < NUM_SENDERS; i++) {
    if (nodeActive[i] && (currentTime - lastNodeTime[i] < NODE_TIMEOUT)) {
      count++;
    } else {
      nodeActive[i] = false;
      if (i < 3) updateTFT(i); // Update TFT only for Nodes 1–3
    }
  }
  return count;
}

void initNodeData() {
  strcpy(resetMessage.a, "RESET");
  for (int i = 0; i < NUM_SAMPLES; i++) {
    resetMessage.roll[i] = 0.0;
    resetMessage.pitch[i] = 0.0;
  }
  resetMessage.reset = true;
  resetMessage.battery_percentage = 0.0;

  for (int i = 0; i < NUM_SENDERS; i++) {
    strcpy(nodeData[i].a, "");
    for (int j = 0; j < NUM_SAMPLES; j++) {
      nodeData[i].roll[j] = 0.0;
      nodeData[i].pitch[j] = 0.0;
    }
    nodeData[i].reset = false;
    nodeData[i].battery_percentage = 0.0;
  }
}

void handleTouch() {
  static unsigned long lastTouchTime = 0;
  int touchValue = touchRead(TOUCH_PIN);
  if (touchValue < TOUCH_THRESHOLD && millis() - lastTouchTime >= 1000) {
    Serial.println("Touch detected, sending RESET to all nodes");
    sendResetToAll();
    lastTouchTime = millis();
  }
}

void setup() {
  Serial.begin(115200);
  delay(1000);

  Serial.println("ESP32 Touch Test");
  Serial.println("ESP32-WROOM Receiver MAC: " + WiFi.macAddress());

  setup_wifi(WiFiSecrets::ssid, WiFiSecrets::pass);
  tlsClient.setCACert(ca_cert);
  mqttClient.setServer(HiveMQ::broker, HiveMQ::port);
  mqttClient.setCallback(mqttCallback);

  initTFT();
  initRGB();
  initESPNow();
  addPeers();
  initNodeData();
}

void loop() {
  if (WiFi.status() != WL_CONNECTED) setup_wifi(WiFiSecrets::ssid, WiFiSecrets::pass);
  if (!mqttClient.connected()) MQTT::reconnect(mqttClient, WiFi.macAddress().c_str(), HiveMQ::username, HiveMQ::password, subscribeTopics, 1);
  mqttClient.loop();

  static unsigned long lastSyncTime = 0;
  unsigned long currentTime = millis();
  if (currentTime - lastSyncTime >= SYNC_INTERVAL) {
    sendSyncBeacon();
    lastSyncTime = currentTime;
  }

  handleTouch();
  delay(10);
}
